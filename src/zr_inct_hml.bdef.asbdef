managed implementation in class zbp_r_inct_hml unique;
strict ( 2 );
with draft;

define behavior for ZR_INCT_HML alias Incident
persistent table zdt_inct_hml
draft table zdt_d_inct_hml
etag master LocalLastChangedAt
lock master total etag LastChangedAt
authorization master ( global, instance )
{

  create;
  update;
  delete;

  association _History { create ( features : instance ); with draft; }

  field ( readonly )
  IncUUID,
  IncidentID,
  Status,
  ChangedDate,
  LocalCreatedBy,
  LocalCreatedAt,
  LocalLastChangedBy,
  LocalLastChangedAt,
  LastChangedAt;

  field ( numbering : managed )
  IncUUID;

  field (mandatory)
   title,
   Description,
   Priority;

  action ( features : instance, authorization : update ) changeStatus
    parameter zdd_change_status_param_hml result [1] $self;

//  side effects { action changeStatus affects $self; }

  internal action setHistory;

  validation validateTitle on save { create; field Title; }
  validation validateDescription on save { create; field Description; }
  validation validatePriority on save { create; field Priority; }
  validation validateCreatedDate on save { create; field CreationDate; }

  determine action validateTitleID { validation validateTitle; }
  determine action validateDescriptionID { validation validateDescription; }
  determine action validatePriorityID { validation validatePriority; }
  determine action validateCreatedDateID { validation validateCreatedDate; }

  side effects {
        action changeStatus affects $self;
        determine action validateTitleID executed on field Title affects messages;
        determine action validateDescriptionID executed on field Description affects messages;
        determine action validatePriorityID executed on field Priority affects messages;
        determine action validateCreatedDateID executed on field CreationDate affects messages;
  }


  determination setDefaultHistory on save { create; }

  determination setDefaultValues on modify { create; }

  draft action Activate optimized;
  draft action Discard;
  draft action Edit;
  draft action Resume;
  draft determine action Prepare;

  mapping for zdt_inct_hml
    {
      IncUUID            = inc_uuid;
      IncidentID         = incident_id;
      Title              = title;
      Description        = description;
      Status             = status;
      Priority           = priority;
      CreationDate       = creation_date;
      ChangedDate        = changed_date;
      LocalCreatedBy     = local_created_by;
      LocalCreatedAt     = local_created_at;
      LocalLastChangedBy = local_last_changed_by;
      LocalLastChangedAt = local_last_changed_at;
      LastChangedAt      = last_changed_at;
    }
}

define behavior for ZDD_INCT_H_hml alias History
implementation in class zbp_dd_inct_h_hml unique
persistent table zdt_inct_h_hml
draft table zdt_d_inct_h_hml
lock dependent by _Incident
authorization dependent by _Incident
etag master LocalLastChangedAt
{
  update;
  delete;

  association _Incident { with draft; }

  field ( numbering : managed )
  HisUUID;

  field ( readonly )
  HisUUID,
  IncUUID,
  LocalCreatedBy,
  LocalCreatedAt,
  LocalLastChangedBy,
  LocalLastChangedAt,
  LastChangedAt;

  mapping for zdt_inct_h_hml
    {
      HisUUID            = his_uuid;
      IncUUID            = inc_uuid;
      HisID              = his_id;
      PreviousStatus     = previous_status;
      NewStatus          = new_status;
      Text               = text;
      LocalCreatedBy     = local_created_by;
      LocalCreatedAt     = local_created_at;
      LocalLastChangedBy = local_last_changed_by;
      LocalLastChangedAt = local_last_changed_at;
      LastChangedAt      = last_changed_at;
    }
}